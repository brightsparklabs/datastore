/*
 * Created by brightSPARK Labs
 * www.brightsparklabs.com
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the java-library plugin to add support for Java Library.
    id 'java-library'

    // Apply the groovy plugin to also add support for Groovy (needed for Spock).
    id 'groovy'

    // Apply the brightSPARK Labs plugin for standardisation.
    id 'com.brightsparklabs.gradle.baseline' version '4.4.0'

    // -----------------------------------------------------------------------------
    // PUBLISHING
    // -----------------------------------------------------------------------------

    id 'maven-publish'
    // Maven Central requires artifacts to be signed.
    id 'signing'

    // Maven Central publishing needs to be fed through Nexus.
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

sourceCompatibility = 17
targetCompatibility = 17

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description = 'Library for storing/retrieving data by id into a backing store'
project.ext.url     = 'https://github.com/brightsparklabs/datastore'
project.ext.scm     = 'scm:git@github.com:brightsparklabs/datastore.git'

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    // Use mavenCentral for resolving dependencies.
    mavenCentral()
}

dependencies {
    def versions = [
        immutablesVersion: '2.9.3',
    ]

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    implementation(
            // General
            'com.google.guava:guava:23.0',
            )

    compileOnly(
            "org.immutables:value:${versions.immutablesVersion}",
            )

    testImplementation(
            // Use the awesome Spock testing and specification framework even with Java
            'org.spockframework:spock-core:2.4-M4-groovy-4.0',
            'org.junit.jupiter:junit-jupiter-api:5.10.3',
            // Allow spock to mock non-interfaces
            'net.bytebuddy:byte-buddy:1.14.17',
            )

    annotationProcessor(
            "org.immutables:value:${versions.immutablesVersion}",
            )
}

// -----------------------------------------------------------------------------
// SETUP ARTIFACTS FOR PUBLISHING
// -----------------------------------------------------------------------------

// Maven Central requires javadoc and sources.
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java

            // Maven Central requires specific POM attributes.
            pom {
                name = project.name
                description = project.description
                url = project.url

                scm {
                    connection = project.scm
                    developerConnection = project.scm
                    url = project.scm
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'brightsparklabs'
                        name = 'brightSPARK Labs'
                        email = 'enquire@brightsparklabs.com'
                    }
                }
            }
        }
    }
}

// Maven Central requires signed artifacts.
signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava

    // Only require signing when publishing (otherwise normal builds would need GPG keys).
    required { gradle.taskGraph.hasTask("publishToSonatype") }
}

// -----------------------------------------------------------------------------
// PUBLISH ARTIFACTS (via nexus)
// -----------------------------------------------------------------------------

nexusPublishing {
    repositories {
        // Credentials default to:
        // - Property `sonatypeUsername` or ENV variable `ORG_GRADLE_PROJECT_sonatypeUsername`.
        // - Property `sonatypePassword` or ENV variable ORG_GRADLE_PROJECT_sonatypePassword`.
        sonatype()
    }
}

task prePublishToMavenCentral {
    group 'brightSPARK Labs - Maven Central Publishing'
    description 'Stages the release on Sonatype ready for publishing to Maven Central.'
    dependsOn 'publishToSonatype', 'closeSonatypeStagingRepository'
}
task publishToMavenCentral {
    group 'brightSPARK Labs - Maven Central Publishing'
    description 'Publishes the release to Maven Central.'
    dependsOn 'publishToSonatype', 'closeAndReleaseSonatypeStagingRepository'
}
